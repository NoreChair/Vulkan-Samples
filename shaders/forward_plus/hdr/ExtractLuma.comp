#version 450 core
precision highp float;

layout(set = 0, binding = 0) uniform Uniforms{
    vec4 extent;
    float targetLuma;
    float adaptationRate;
    float minExposure;
    float maxExposure;
    uint pixelCount;
};

layout(set = 0, std430, binding = 1) readonly buffer ExposureBuffer{
    // from 0 to the end
    // exposure, 1.0/exposure, exposure, 0.0, initMinLog, initMaxLog, range, rcpRange
    float exposureBuffer[];
};
layout(set = 0, binding = 2) uniform sampler2D sceneColor;
layout(set = 0, r8ui, binding = 3) writeonly uniform highp uimage2D lumaResult;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

float RGBToLuminance(vec3 rgb){
    return dot(rgb, vec3(0.212671, 0.715160, 0.072169));
}

void main(){
    uvec2 dispatchID = min(gl_GlobalInvocationID.xy, uvec2(extent.xy));
    vec2 uv = vec2(dispatchID) * extent.zw;
    vec2 offset = extent.zw * 1.0;
    // sampler must be clamp to edge
    // sample 4x4 grid, calc luma average
    vec3 sampleColor0 = texture(sceneColor, uv + vec2(-offset.x, -offset.y)).rgb;
    vec3 sampleColor1 = texture(sceneColor, uv + vec2(-offset.x, offset.y)).rgb;
    vec3 sampleColor2 = texture(sceneColor, uv + vec2(offset.x, -offset.y)).rgb;
    vec3 sampleColor3 = texture(sceneColor, uv + offset).rgb;
    float luma = RGBToLuminance(sampleColor0 + sampleColor1 + sampleColor2 + sampleColor3) * 0.25;

    if(luma == 0.0){
        imageStore(lumaResult, ivec2(dispatchID), uvec4(luma));
    }else{
        float minLog = exposureBuffer[4];
        float rcpLogRange = exposureBuffer[7];
        float logLuma = clamp((log2(luma) - minLog) * rcpLogRange, 0.0, 1.0);
        imageStore(lumaResult, ivec2(dispatchID), uvec4(logLuma * 254.0 + 1.0));
    }
}